 R←Test_ListDirs_001(stopFlag batchFlag);⎕TRAP;list
⍝ Test the `ListDirs` method
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 list←##.FilesAndDirs.ListDirs ∆Home
 →T.PassesIf 2=≢list
 →T.PassesIf∧/##.FilesAndDirs.IsDir¨list

 list←'recursive'##.FilesAndDirs.ListDirs ∆Home
 →T.PassesIf 2<≢list
 →T.PassesIf∧/##.FilesAndDirs.IsDir¨list

 →T.PassesIf 2=≢##.FilesAndDirs.ListDirs ∆Home,'/Second/Sub*'

 :Trap 11
     {}'Recursive'##.FilesAndDirs.ListDirs ∆Home,'/Second/Sub*'
     →T.FailsIf 1
 :Else
     →T.PassesIf'"path" must not carry wildcard chars in case "Recursive" is specified'≡⊃⎕DM
 :EndTrap

 :Trap 11
     {}##.FilesAndDirs.ListDirs ∆Home,'/Sec*/Sub'
     →T.FailsIf 1
 :Else
     →T.PassesIf'Wildcard characters are allowed only in the last part of a path'≡⊃⎕DM
 :EndTrap

 R←T._OK
