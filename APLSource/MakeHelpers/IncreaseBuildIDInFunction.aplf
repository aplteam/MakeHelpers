 {version}←{isTest}IncreaseBuildIDInFunction fnsName;buildID;result;buff;name
⍝ Increases the build number of `fnsName` which must be the fully qualified name of a "Version" function
⍝ that lives either as a public method in a class or a trad-function in a namespace or namespace script.\\
⍝ The build number is increased by 1, the modified script or function fixed and Link is notified.\\
⍝ Returns the new version string as simple character vector or ''.\\
⍝ The version function may return one of:
⍝ * A simple character vector with the version number
⍝ * A nested vector of length two: version number and version date like '1.2.3' '2023-03-01'
⍝ * A nested vector of length three: name, version number and version date\\
⍝ The left argument is used only by test cases.
 version←''
 isTest←{0=⎕NC ⍵:0 ⋄ ⍎⍵}'isTest'
 :If 0<⎕NC⊂fnsName
     name←'.'A.Last fnsName
 :OrIf 0≠(⍎(¯1+-≢name)↓fnsName).⎕NC⊂name  ⍝ shared method?
     ('Is not a tradFns: ',fnsName)Assert 3.1=|⎕NC⊂fnsName
     :If 2=≡buff←⍎fnsName
         version←(1+3=≢buff)⊃buff
         :If '+'∊version
             buildID←1+A.ToNum 2⊃'+'(≠⊆⊢)version
         :Else
             '"version" must carry precisely two dots'Assert 2='.'+.=version
             buildID←'1'
         :EndIf
         version←fnsName AddBuildIDtoVersionNested buildID
     :Else
         ⍝ Supposed to be a simple character string like '1.2.3' or '1.2.3+456' 'or '1.2.3-beta-4+5678'
         version←buff
         '"version" must carry precisely two dots'Assert 2='.'+.=version
         :If '+'∊version
             buildID←1+A.ToNum{⍵↓⍨⍵⍳'+'}version
         :Else
             buildID←'1'
         :EndIf
         version←fnsName AddBuildIDtoVersionSimple buildID
     :EndIf
     :If ~isTest
         :If ¯1=×⎕NC⊂fnsName ⍝ Member of a class?
         :OrIf {0::0 ⋄ 1⊣⎕SRC⍎⍵}¯1↓1⊃'.'A.SplitPath fnsName
             {}⎕SE.Link.Add ¯1↓1⊃'.'A.SplitPath fnsName
         :Else
             {}⎕SE.Link.Add⍕fnsName
         :EndIf
     :EndIf
 :EndIf
⍝Done
