 {r}←GetLaterUserCommand(uc cfg C path batch);list;bool;fn1;fn2;caption1;caption2;cfg;cfgFilename;json;groupAndName
⍝ Check whether the user command `uc` is "younger" in `path` than in the project.\\
⍝ If so then the user is asked whether she wants to copy that version over, unless `batch` is 1;
⍝ in that case the copy operation is carried out silently.
⍝ * `uc` is the name of the user command file
⍝ * `C` is a namespace with a Cider configuration
⍝ * `path` is something like the Dyalog MyUCMDs/ folder
⍝ * `batch` is a boolean indicating the function is called as part of a batch make,
⍝   meaning that no user is available in front of the monitor
⍝ Returns 0 for "no action", 1 for the file in `path` winning the day and 2 if the project's version is younger.
 r←0
 :If 0<≢list←(1+≢path)↓¨F.ListDirs path
     groupAndName←cfg.group,'-',cfg.name
 :AndIf 0<+/bool←groupAndName{⍺∘≡¨(≢⍺)↑¨⍵}list
     list←'recursive'F.ListFiles path,'/',(bool⍳1)⊃list
     bool←∨/¨uc∘⍷¨list
     'Could not locate the user command script?!'Assert 1=+/bool
     fn1←(bool⍳1)⊃list
     fn2←C.HOME,'/',cfg.source,'/',uc
     caption1←path
     caption2←C.CIDER.source,'/'
     r←batch CopyBetter fn1 fn2 caption1 caption2
 :EndIf
⍝Done
