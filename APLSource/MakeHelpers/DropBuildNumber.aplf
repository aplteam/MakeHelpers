 version←DropBuildNumber version;b;ind
⍝ Takes one of:
⍝ * A text vector that is a version number, possibly with a build number
⍝ * A nested vector of length 2 with the version number in the first item and YYYY-MM-DD in the second
⍝ * A nested vector of length 3 with name, version and date
⍝ Returns what it got but without a build number
⍝ Examples:
⍝ * `'1.2.3' ← DropBuildNumber '1.2.3'`
⍝ * `'1.2.3' ← DropBuildNumber '1.2.3+1234'`
⍝ * `'1.2.3' ← DropBuildNumber '1.2.3' '2019-03-07'`
⍝ * `'1.2.3' ← DropBuildNumber '1.2.3+1234' '2019-03-07'`
⍝ * `'1.2.3' ← DropBuildNumber 'Foo' '1.2.3' '2019-03-07'`
⍝ * `'1.2.3' ← DropBuildNumber 'Foo' '1.2.3+1234' '2019-03-07'`
 :If 1<≡version
     ind←1+3=≢version
     (ind⊃version)←DropBuildNumber ind⊃version
 :ElseIf '+'∊version
     version←(¯1+version⍳'+')↑version
 :EndIf
⍝Done
