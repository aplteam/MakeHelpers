 {r}←{batch}FetchLaterUserCommand(cfg C path);fn2;caption1;caption2;cfg;cfgFilename;json;buff;fn1;uc;path2
⍝ Check if the user command in `path` (if any) is identical with the one in the project. If they are
⍝ not identical the user is asked whether she wants to copy the later version over, unless `batch` is
⍝ 1, in which case the copy operation is carried out silently.
⍝ * `C` is a namespace with a Cider configuration
⍝ * `path` is something like the Dyalog MyUCMDs/ folder, though `[myucmds]` is understood and replaced\\
⍝   You only need to add the name of the sub folder if it is not identical with the user command name
⍝ * `batch`, if specified, must be a boolean indicating the function is called as part of an
⍝   automated "make" process, meaning that no user is available in front of the monitor\\
⍝ Returns 0 for "no action", 1 for the file in `path` winning the day and 2 otherwise.\\
⍝ If the script is copied over from `path` and batch←0 then the user is warned that the WS is
⍝ out of sync in respect to the user command script.\\
⍝ Because we can savely assume that there should be such a folder hosting such a script, we ask the user
⍝ whether she wants to carry on anyway if we can't find it.
 r←0
 batch←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'batch'
 :If 0<≢uc←{⊃,/1↓⎕NPARTS ⍵}cfg.userCommandScript
     :If '[myucmds]'{⍺≡⎕C(≢⍺)↑⍵}path
         :If 0=≢path2←{⍵↓⍨⍵⍳']'}path
             path2←cfg.name
         :EndIf
         path←F.AddTrailingSep 1 F.NormalizePath(GetMyUCMDsFolder''),'/',path2
     :EndIf
     fn1←(FilesAndDirs.AddTrailingSep path),uc
     :If F.IsFile fn1
         caption1←path
         caption2←C.CIDER.source,'/'
         buff←{⍵↑⍨(≢⍵)⌊⌊/⍵⍳'/\'}cfg.source
         fn2←C.HOME,'/',buff,'/',uc
         :If cfg.source≢buff
             (uc,' not found in neither ',path,cfg.name,' nor ',buff)Assert ⎕NEXISTS fn2
         :Else
             (uc,' not found in ',cfg.source)Assert ⎕NEXISTS fn2
         :EndIf
         caption2←buff
         r←batch CopyBetter fn1 fn2 caption1 caption2
     :Else
         'Interrupted on behalf of the user'Assert CommTools.YesOrNo'Could not locate the user command script ',fn1,'; carry on?'
     :EndIf
     :If ~batch
     :AndIf r=¯1
         Frame'<',uc,'> on disk is now out of sync with the WS!'
     :EndIf
 :EndIf
⍝Done
