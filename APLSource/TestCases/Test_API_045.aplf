 R←Test_API_045(stopFlag batchFlag);⎕TRAP;qdmx
⍝ Exercise IncreaseBuildIDInFunction on a function that returns a simple character string
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''

 #.TEMP.⎕FX'r←Version' 'r←''1.0.0'''
 1 ##.MakeHelpers.IncreaseBuildIDInFunction'#.TEMP.Version'
 →T.FailsIf'1.0.0+1'≢#.TEMP.Version
 1 ##.MakeHelpers.IncreaseBuildIDInFunction'#.TEMP.Version'
 →T.FailsIf'1.0.0+2'≢#.TEMP.Version

 #.TEMP.⎕FX'r←Version' 'r←''1.0.0+3'''
 1 ##.MakeHelpers.IncreaseBuildIDInFunction'#.TEMP.Version'
 →T.FailsIf'1.0.0+4'≢#.TEMP.Version
 1 ##.MakeHelpers.IncreaseBuildIDInFunction'#.TEMP.Version'
 →T.FailsIf'1.0.0+5'≢#.TEMP.Version

 #.TEMP.⎕FX'r←Version' 'r←''MakerTest'' ''1.0.0.3'' ''2019-01-05'''
 :Trap 11
     1 ##.MakeHelpers.IncreaseBuildIDInFunction'#.TEMP.Version'
     →T.FailsIf 1
 :Else
     qdmx←⎕DMX
     →T.PassesIf'"version" must carry precisely two dots'≡qdmx.EM
 :EndTrap

 #.TEMP.⎕FX'r←Version' 'r←''1.0.0-beta-1+3'''
 1 ##.MakeHelpers.IncreaseBuildIDInFunction'#.TEMP.Version'
 →T.FailsIf'1.0.0-beta-1+4'≢#.TEMP.Version
 1 ##.MakeHelpers.IncreaseBuildIDInFunction'#.TEMP.Version'
 →T.FailsIf'1.0.0-beta-1+5'≢#.TEMP.Version

 R←T._OK
⍝Done
