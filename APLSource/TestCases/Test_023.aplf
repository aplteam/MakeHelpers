 R←Test_023(stopFlag batchFlag);⎕TRAP;cfg;toPath;C;fromPath;script;pkgName;script1;script2
⍝ Exercise GetLaterUserCommand
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 fromPath←FilesAndDirs.GetTempSubDir''
 toPath←FilesAndDirs.GetTempSubDir''
 fromPath ⎕NCOPY ##.CiderConfig.HOME,'/Tests/SomeProject/cider.config'
 fromPath ⎕NCOPY ##.CiderConfig.HOME,'/Tests/SomeProject/APLSource'
 fromPath ⎕NCOPY ##.CiderConfig.HOME,'/Tests/SomeProject/apl-package.json'
 C←⎕JSON⍠('Dialect' 'JSON5')⊣⊃FilesAndDirs.NGET fromPath,'/cider.config'
 C.HOME←fromPath
 cfg←##.MakeHelpers.GetPackageCfg fromPath
 pkgName←cfg.(group,'-',name,'-',version)
 'Create!'FilesAndDirs.CheckPath toPath,'/',pkgName
 script←⊃FilesAndDirs.NGET(fromPath,'/APLSource/My_UC.dyalog')1
 script←(script[1]),(⊂'⍝ Says "Hello"'),1↓script
 ⎕DL 1
 (⊂script)FilesAndDirs.NPUT(toPath,'/',pkgName,'/My_UC.dyalog')1 ⍝ That makes it younger
 →T.GoToTidyUp 1≠##.MakeHelpers.GetLaterUserCommand'My_UC.dyalog'cfg C toPath 1

 script1←⊃FilesAndDirs.NGET toPath,'/',pkgName,'/My_UC.dyalog'
 script2←⊃FilesAndDirs.NGET fromPath,'/APLSource/My_UC.dyalog'
 Assert∨/'⍝ Says "Hello"'⍷script1
 →T.GoToTidyUp script1≢script2

 ⎕DL 1
 script←(script[1]),2↓script ⍝ Remove the comment line
 (⊂script)FilesAndDirs.NPUT(fromPath,'/APLSource/My_UC.dyalog')1 ⍝ That makes it younger again
 →T.GoToTidyUp 2≠##.MakeHelpers.GetLaterUserCommand'My_UC.dyalog'cfg C toPath 1
 script1←⊃FilesAndDirs.NGET toPath,'/',pkgName,'/My_UC.dyalog'
 script2←⊃FilesAndDirs.NGET fromPath,'/APLSource/My_UC.dyalog'
 Assert 0=+/'⍝ Says "Hello"'⍷script1
 →T.GoToTidyUp script1≢script2

 ⎕DL 1
 →T.GoToTidyUp 0≠##.MakeHelpers.GetLaterUserCommand'My_UC.dyalog'cfg C toPath 1

 R←T._OK

∆TidyUp:
 {}FilesAndDirs.RmDirByForce fromPath
 {}FilesAndDirs.RmDirByForce toPath
⍝Done
