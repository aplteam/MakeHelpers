 R←Test_015(stopFlag batchFlag);⎕TRAP;tempFolder;parms;res;expected
⍝ Exercise CreateTatinPackageFromSingleScript on a package without dependencies
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempFolder←(FilesAndDirs.GetTempSubDir'Maker_',⊃⎕SI),'/'
 (##.CiderConfig.HOME,'/Tests/SingleScriptPackage/APLTreeUtils2')##.MakeHelpers.CopyTo tempFolder
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 parms←⎕SE.Cider.CreateOpenParms''
 parms.folder←tempFolder,'APLTreeUtils2'
 parms.watch←'ns'
 parms.parent←'#.TEMP'
 parms.projectSpace←''
 parms.quietFlag←1
 parms.importFlag←1
 Assert ⎕SE.Cider.OpenProject parms

 1 ##.MakeHelpers.CreateTatinPackageFromSingleScript #.TEMP.APLTreeUtils2.APLTreeUtils2
 res←⊃FilesAndDirs.Dir parms.folder,'/Dist/'
 res/⍨←'.zip'∘≢¨3⊃∘⎕NPARTS¨res
 expected←'apl-package.json' 'APLTreeUtils2.aplc'
 →T.GoToTidyUp expected≢{⊃,/1↓⎕NPARTS ⍵}¨res

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ {}FilesAndDirs.RmDirByForce tempFolder ⋄ :EndTrap
⍝Done
