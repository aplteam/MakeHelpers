 R←Test_API_011(stopFlag batchFlag);⎕TRAP;noOf;folder;res
⍝ Exercise CreateAPI on all types of objects with a namespace that is not linked
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 folder←##.CiderConfig.HOME,'/Tests/Myapp'
 res←⎕SE.Link.Import'#.TEMP'folder
 Assert∨/'Imported:'⍷res
 Assert~∨/'Error:'⍷res

 noOf←##.MakeHelpers.CreateAPI #.TEMP(↓#.TEMP.⎕NL⍳16)
 →T.PassesIf noOf=≢(' '~⍨¨↓#.TEMP.⎕NL⍳16)~⊂'API'

 →T.PassesIf'Universe'≡#.TEMP.API.HelloVars
 →T.PassesIf'Galaxy'≡#.TEMP.API.class1.Hello2
 →T.PassesIf'Niladic'≡#.TEMP.API.fn1
 →T.PassesIf'Monadic'≡#.TEMP.API.fn2 ⍬
 →T.PassesIf'Dyadic'≡⍬ #.TEMP.API.fn3 ⍬
 →T.PassesIf'Ambivalent'≡#.TEMP.API.fn4 ⍬
 →T.PassesIf'Ambivalent'≡⍬ #.TEMP.API.fn4 ⍬
 →T.PassesIf(,9)≡#.TEMP.API.⎕NC'interface1'
 →T.PassesIf'World'≡#.TEMP.API.ns.Hello
 →T.PassesIf 4∧.=#.TEMP.API.⎕NC'op1'
 →T.PassesIf 2∧.=#.TEMP.API.⎕NC↑'HelloVars' 'Hello'
 →T.PassesIf 0=≢FilesAndDirs.ListDirs folder,'/'
 →T.PassesIf'World'≡#.TEMP.API.Hello

 R←T._OK
⍝Done
