 R←Test_API_116(stopFlag batchFlag);⎕TRAP;C;tempFolder;F;CFG;report;buff;expected
⍝ Exercise ##.MakeHelpers.CreateNoticeFile but append to an existing NOTICE.md
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 C←##.CiderConfig ⋄ F←FilesAndDirs
 tempFolder←F.GetTempSubDir⊃⎕SI
 F.MkDir tempFolder,'/MarkAPL'
 (tempFolder,'/MarkAPL/')⎕NCOPY C.HOME,'/Tests/MarkAPL/packages'
 (tempFolder,'/MarkAPL/')⎕NCOPY C.HOME,'/Tests/MarkAPL/packages_dev'
 (tempFolder,'/MarkAPL/')⎕NCOPY C.HOME,'/Tests/MarkAPL/apl-package.json'
 (tempFolder,'/MarkAPL/')⎕NCOPY C.HOME,'/Tests/MarkAPL/cider.config'

 CFG←⎕NS''
 CFG.HOME←tempFolder,'/MarkAPL'
 'CIDER'CFG.⎕NS''
 'CIDER.dependencies'CFG.⎕NS''
 CFG.CIDER.dependencies.tatin←'packages'
 CFG.CIDER.distributionFolder←'Dist'
 ⎕MKDIR CFG.HOME,'/',CFG.CIDER.distributionFolder
 (⊂'## firlefanz')⎕NPUT CFG.HOME,'/',CFG.CIDER.distributionFolder,'/NOTICE.md'

 report←0 ##.MakeHelpers.CreateNoticeFile(⊂CFG.HOME,'/'),¨CFG.CIDER.dependencies.tatin CFG.CIDER.distributionFolder
 →T.GoToTidyUp 1≠≢report
 →T.GoToTidyUp'aplteam-OS-3.0.1'≢∊report

 buff←⊃⎕NGET(CFG.HOME,'/',CFG.CIDER.distributionFolder,'/NOTICE.md')1
 expected←{⊃,/1↓⎕NPARTS ⍵}¨F.ListDirs2 CFG.HOME,'/',CFG.CIDER.dependencies.tatin,'/'
 expected~←⊂'aplteam-OS-3.0.1'
 expected,←⊂'firlefanz'
 →T.GoToTidyUp~expected ∆LazyMatch'## '{(≢⍺)↓¨⍵/⍨⍺∘≡¨(≢⍺)↑¨⍵}buff

 R←T._OK

∆TidyUp:
 {}FilesAndDirs.RmDirByForce tempFolder
⍝Done
