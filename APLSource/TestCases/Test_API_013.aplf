 R←Test_API_013(stopFlag batchFlag);⎕TRAP;noOf;folder1;folder2;list;parms
⍝ Exercise CreateAPI on a project with sub-namespaces in the API
⍝ Originally sub-namespaces ween't even allowed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 folder1←##.CiderConfig.HOME,'/Tests/CompareFiles'
 folder2←FilesAndDirs.GetTempSubDir⊃⎕SI
 (folder1,'/*')##.MakeHelpers.CopyTo folder2,'/'
 parms←⎕NS''
 parms.watch←'ns'
 {}parms ⎕SE.Link.Create'#.TEMP'folder2

 noOf←##.MakeHelpers.CreateAPI #.TEMP.APLSource.CompareFiles #.TEMP.APLSource.CompareFiles.Public

 →T.GoToTidyUp noOf≠10
 →T.GoToTidyUp 9≠#.TEMP.APLSource.CompareFiles.API.⎕NC'ComparisonTools'
 →T.GoToTidyUp 1≠≢#.TEMP.APLSource.CompareFiles.API.ComparisonTools.⎕nl 3

 R←T._OK

∆TidyUp:
 {}⎕SE.Link.Break'#.TEMP'
 :Trap 0 ⋄ {}FilesAndDirs.RmDirByForce folder2 ⋄ :EndTrap
⍝Done
