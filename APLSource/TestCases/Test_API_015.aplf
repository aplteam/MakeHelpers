 R←Test_API_015(stopFlag batchFlag);⎕TRAP;noOf
⍝ Exercise CreateAPI on a namespace with three functions (niladic,monadic,dyadic) with shy results
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If batchFlag
     R←T._NoBatchTest
     :Return
 :EndIf

 0 0⍴⍞,0/⎕←'This test needs you: nothing must be printed to the session after this message; to continue press <enter>'

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 {' '≠1↑0⍴⍵:.}#.TEMP.⎕FX'{r}←Hello' 'r←''World'''
 {' '≠1↑0⍴⍵:.}#.TEMP.⎕FX'{r}←x Plus y' 'r←x+y'
 {' '≠1↑0⍴⍵:.}#.TEMP.⎕FX'{r}←Times y' 'r←×/y'
 {' '≠1↑0⍴⍵:.}#.TEMP.⎕FX'r←Public' 'r←''Hello'' ''Plus'' ''Times'''
 noOf←##.MakeHelpers.CreateAPI #.TEMP #.TEMP.Public'#.TEMP.API'
 Assert noOf=≢#.TEMP.Public
 Assert #.TEMP.Public ∆LazyMatch(⊂'Public')~⍨' '~⍨¨↓#.TEMP.⎕NL 3 4
 #.TEMP.API.Hello
 3 #.TEMP.API.Plus 2
 #.TEMP.API.Times 2 3

 →T.PassesIf CommTools.YesOrNo'Was none of "Hello", "5" and "6" printed to the session?'

 R←T._OK
⍝Done
