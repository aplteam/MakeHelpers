 R←Test_001(stopFlag batchFlag);⎕TRAP;F;path;zipFilename1;zipFilename2;zipFilename3
⍝ Exercise CreateZipFile
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 F←FilesAndDirs

 path←F.GetTempFilename2''
 F.DeleteFile path
 path←¯4↓path
 Assert'Create!'F.CheckPath path
 (⊂'this')⎕NPUT path,'\file1.txt'
 (⊂'that')⎕NPUT path,'\file2.txt'
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX'r←Version' 'r←''Foo'' ''0.1.0'' ''2019-02-19'''
 zipFilename1←##.MakeHelpers.CreateZipFile'Foo'path(2⊃#.TEMP.Version)
 →T.GoToTidyUp≢/F.NormalizePath(path,'/')(1⊃⎕NPARTS zipFilename1)
 →T.GoToTidyUp'Foo-0.1.0.zip'≢⊃,/1↓⎕NPARTS zipFilename1
 zipFilename2←##.MakeHelpers.CreateZipFile'Foo'path''
 →T.GoToTidyUp'Foo.zip'≢⊃,/1↓⎕NPARTS zipFilename2
 zipFilename3←##.MakeHelpers.CreateZipFile'Foo'path'1.2.3-beta-2+9876'
 →T.GoToTidyUp'Foo-1.2.3-beta-2.zip'≢⊃,/1↓⎕NPARTS zipFilename3

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDirByForce path ⋄ :EndTrap
⍝Done
