 R←Test_016(stopFlag batchFlag);⎕TRAP;tempFolder;parms;res;expected
⍝ Exercise CreateTatinPackageFromSingleScript on a pakage with dependencies
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempFolder←(FilesAndDirs.GetTempSubDir'Maker_',⊃⎕SI),'/'
 (##.CiderConfig.HOME,'/Tests/SingleScriptPackageWithDeps/HandleError')##.MakeHelpers.CopyTo tempFolder
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 parms←⎕SE.Cider.CreateOpenParms''
 parms.folder←tempFolder,'HandleError'
 parms.watch←'ns'
 parms.projectSpace←#.TEMP
 parms.quietFlag←1
 parms.importFlag←1
 Assert ⎕SE.Cider.OpenProject parms

 1 ##.MakeHelpers.CreateTatinPackageFromSingleScript #.TEMP.HandleError
 res←⊃FilesAndDirs.Dir parms.folder,'/Dist/'
 res/⍨←'.zip'∘≢¨3⊃∘⎕NPARTS¨res
 expected←'apl-dependencies.txt' 'apl-package.json' 'HandleError.aplc'

 →T.GoToTidyUp expected≢{⊃,/1↓⎕NPARTS ⍵}¨res

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ {}FilesAndDirs.RmDir tempFolder ⋄ :EndTrap
⍝Done
